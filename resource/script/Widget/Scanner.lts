function Int Resolution () 256

type WidgetScanner
  Player player
  Object object
  ScannerOutput output
  Sound soundLoop Sound

  function Void Create (Widget self)
    Sound_Play "ui/scanneropen.wav" 0.9
    soundLoop = (Sound_PlayLooped "scanner/loop.wav" 0.3)
    
  function Void Destroy (Widget self)
    Sound_Play "ui/scannerclose.wav" 0.4
    soundLoop.Delete

  function Void PreDraw (Widget self)
    var dx 0.2 * self.size.x / (Float output.Size)
    var dy 0.25 * self.size.y
    var y self.pos.y + self.size.y
    var color 2.0 * Colors:Primary

    var last (Vec2 0)
    for i 0 i < output.Size (++ i)
      var x self.pos.x + self.size.x * (i / (output.Size - 1))
      var v 1.0 - (ExpDecay (output.Get i) 2.5)
      var p1 (Vec2 x y)
      var p2 p1 - v * (Vec2 0 dy)
      var p3 p1 - 0.2 * v * (Vec2 0 dy)
      Draw (Glyph_Box 0 1 1 1) 0.5 * (p1 + p2) (Vec2 0.45 * self.size.x / (cast Float output.Size - 1) 0.5 * (Abs p2.y - p1.y)) color 0.05
      if i > 0
        Draw (Glyph_Line last p2 1 1) 0 1 color 0.5
      # Draw (Glyph_Circle 0 1 1 1) p2 16 color 1.0
      last = p2

    var font Fonts:Subheading
    for i 0 i < 8 (++ i)
      var x self.pos.x + self.size.x * (i / 7)
      var y self.pos.y + self.size.y
      Draw (Glyph_LineFade (Vec2 x y) (Vec2 x y + 16.0) 1 1) 0 1 color 1
      DrawText font "10" (Vec2 x y + 32.0) 20 1 1 true
      DrawText font i (Vec2 x + 16.0 y + 24.0) 15 1 1 true

    DrawText font "frequency"
      Vec2 (self.pos.x + self.size.x / 2.0) (self.pos.y + self.size.y + 24.0)
      20
      1
      1
      true

    Draw (Glyph_Line self.BottomLeft self.BottomRight 1 1) 0 1 color 0.3

  function Void PreUpdate (Widget self)
    var newOutput (Object_GetScannerOutput object Resolution)
    output.Blend newOutput (1.0 - (ExpDecay FrameTimer_Get 0.1))
    soundLoop.SetVolume 0.1 + 0.5 * (1.0 - (ExpDecay output.AverageAmplitude 1.0))
    soundLoop.SetPitch 0.5 + output.AverageFrequency

function Widget Create (Player player Object object)
  Custom Widget
    WidgetScanner player object (Object_GetScannerOutput object Resolution)
